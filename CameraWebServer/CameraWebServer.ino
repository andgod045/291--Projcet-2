#include "esp_camera.h"
#include <WiFi.h>
#include <WiFiUDP.h>

#include "board_config.h"

const char *ssid = "beepboop";
const char *password = "beepboop";

#define TRACK_EN_PIN   12
#define TRACK_CMD0_PIN 13
#define TRACK_CMD1_PIN 15
#define UDP_PORT 1234

WiFiUDP udp;

bool trackingActive = false;

void startCameraServer();
void setupLedFlash();

void trackingDisable() {
  digitalWrite(TRACK_EN_PIN, LOW);
  digitalWrite(TRACK_CMD0_PIN, LOW);
  digitalWrite(TRACK_CMD1_PIN, LOW);
}

void trackingSendStop() {
  digitalWrite(TRACK_EN_PIN, HIGH);
  digitalWrite(TRACK_CMD0_PIN, LOW);
  digitalWrite(TRACK_CMD1_PIN, LOW);
}

void trackingSendLeft() {
  digitalWrite(TRACK_EN_PIN, HIGH);
  digitalWrite(TRACK_CMD0_PIN, HIGH);
  digitalWrite(TRACK_CMD1_PIN, LOW);
}

void trackingSendRight() {
  digitalWrite(TRACK_EN_PIN, HIGH);
  digitalWrite(TRACK_CMD0_PIN, LOW);
  digitalWrite(TRACK_CMD1_PIN, HIGH);
}

void trackingSendForward() {
  digitalWrite(TRACK_EN_PIN, HIGH);
  digitalWrite(TRACK_CMD0_PIN, HIGH);
  digitalWrite(TRACK_CMD1_PIN, HIGH);
}

void setup() {
  Serial.begin(115200);
  Serial.setDebugOutput(true);
  Serial.println();

  // GPIO outputs to EFM8
  pinMode(TRACK_EN_PIN, OUTPUT);
  pinMode(TRACK_CMD0_PIN, OUTPUT);
  pinMode(TRACK_CMD1_PIN, OUTPUT);

  // Safe startup state
  trackingDisable();

  camera_config_t config;
  config.ledc_channel = LEDC_CHANNEL_0;
  config.ledc_timer = LEDC_TIMER_0;
  config.pin_d0 = Y2_GPIO_NUM;
  config.pin_d1 = Y3_GPIO_NUM;
  config.pin_d2 = Y4_GPIO_NUM;
  config.pin_d3 = Y5_GPIO_NUM;
  config.pin_d4 = Y6_GPIO_NUM;
  config.pin_d5 = Y7_GPIO_NUM;
  config.pin_d6 = Y8_GPIO_NUM;
  config.pin_d7 = Y9_GPIO_NUM;
  config.pin_xclk = XCLK_GPIO_NUM;
  config.pin_pclk = PCLK_GPIO_NUM;
  config.pin_vsync = VSYNC_GPIO_NUM;
  config.pin_href = HREF_GPIO_NUM;
  config.pin_sccb_sda = SIOD_GPIO_NUM;
  config.pin_sccb_scl = SIOC_GPIO_NUM;
  config.pin_pwdn = PWDN_GPIO_NUM;
  config.pin_reset = RESET_GPIO_NUM;
  config.xclk_freq_hz = 20000000;
  config.frame_size = FRAMESIZE_UXGA;
  config.pixel_format = PIXFORMAT_JPEG;
  config.grab_mode = CAMERA_GRAB_WHEN_EMPTY;
  config.fb_location = CAMERA_FB_IN_PSRAM;
  config.jpeg_quality = 12;
  config.fb_count = 1;

  if (config.pixel_format == PIXFORMAT_JPEG) {
    if (psramFound()) {
      config.jpeg_quality = 10;
      config.fb_count = 2;
      config.grab_mode = CAMERA_GRAB_LATEST;
    } else {
      config.frame_size = FRAMESIZE_SVGA;
      config.fb_location = CAMERA_FB_IN_DRAM;
    }
  } else {
    config.frame_size = FRAMESIZE_240X240;
  }

#if defined(CAMERA_MODEL_ESP_EYE)
  pinMode(13, INPUT_PULLUP);
  pinMode(14, INPUT_PULLUP);
#endif

  esp_err_t err = esp_camera_init(&config);
  if (err != ESP_OK) {
    Serial.printf("Camera init failed with error 0x%x\n", err);
    return;
  }

  sensor_t *s = esp_camera_sensor_get();
  if (s->id.PID == OV3660_PID) {
    s->set_vflip(s, 1);
    s->set_brightness(s, 1);
    s->set_saturation(s, -2);
  }
  if (config.pixel_format == PIXFORMAT_JPEG) {
    s->set_framesize(s, FRAMESIZE_QVGA);
  }

#if defined(CAMERA_MODEL_M5STACK_WIDE) || defined(CAMERA_MODEL_M5STACK_ESP32CAM)
  s->set_vflip(s, 1);
  s->set_hmirror(s, 1);
#endif

#if defined(CAMERA_MODEL_ESP32S3_EYE)
  s->set_vflip(s, 1);
#endif

#if defined(LED_GPIO_NUM)
  setupLedFlash();
#endif

  WiFi.begin(ssid, password);
  WiFi.setSleep(false);

  Serial.print("WiFi connecting");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println();
  Serial.println("WiFi connected");

  startCameraServer();
  udp.begin(UDP_PORT);
  Serial.println("UDP command server started on port 1234");

  Serial.print("Camera Ready! Use 'http://");
  Serial.print(WiFi.localIP());
  Serial.println("' to connect");
}

void loop() {
  int packetSize = udp.parsePacket();
  if (packetSize > 0) {
    uint8_t cmd;
    int n = udp.read(&cmd, 1);

    if (n == 1) {
      Serial.printf("CMD: 0x%02X\n", cmd);

      switch (cmd) {
        case 0x03: // TRACKING mode
          trackingActive = true;
          trackingSendStop();
          Serial.println("Tracking enabled");
          break;

        case 0x01: // RC mode
        case 0x02: // GUIDE_WIRE mode
        case 0x04: // STOP mode
          trackingActive = false;
          trackingDisable();
          Serial.println("Tracking disabled");
          break;

        case 0x10: // LEFT
          if (trackingActive) {
            trackingSendLeft();
            Serial.println("Tracking LEFT");
          }
          break;

        case 0x11: // RIGHT
          if (trackingActive) {
            trackingSendRight();
            Serial.println("Tracking RIGHT");
          }
          break;

        case 0x12: // STRAIGHT
          if (trackingActive) {
            trackingSendForward();
            Serial.println("Tracking FORWARD");
          }
          break;

        case 0x13: // STOP
          if (trackingActive) {
            trackingSendStop();
            Serial.println("Tracking STOP");
          }
          break;

        default:
          Serial.println("Unknown command");
          break;
      }
    }
  }

  delay(1);
}